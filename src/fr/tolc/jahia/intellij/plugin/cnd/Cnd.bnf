{
  parserClass="fr.tolc.jahia.intellij.plugin.cnd.parser.CndParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Cnd"
  psiImplClassSuffix="Impl"
  psiPackage="fr.tolc.jahia.intellij.plugin.cnd.psi"
  psiImplPackage="fr.tolc.jahia.intellij.plugin.cnd.psi.impl"

  elementTypeHolderClass="fr.tolc.jahia.intellij.plugin.cnd.psi.CndTypes"
  elementTypeClass="fr.tolc.jahia.intellij.plugin.cnd.psi.CndElementType"
  tokenTypeClass="fr.tolc.jahia.intellij.plugin.cnd.psi.CndTokenType"

  psiImplUtilClass="fr.tolc.jahia.intellij.plugin.cnd.psi.util.CndPsiImplUtil"
}

cnd ::= (COMMENT|CRLF)* (namespace CRLF)* (nodeType | COMMENT | CRLF)*



namespace ::= LEFT_ANGLE_BRACKET namespaceIdentifier EQUAL SINGLE_QUOTE NAMESPACE_URI SINGLE_QUOTE RIGHT_ANGLE_BRACKET
{ mixin="fr.tolc.jahia.intellij.plugin.cnd.psi.impl.CndNamespaceElementImpl"
  implements="fr.tolc.jahia.intellij.plugin.cnd.psi.CndNamespaceElement" 
  methods=[getNamespaceName setNamespaceName getNamespaceURI getPresentation] }
namespaceIdentifier ::= NAMESPACE_NAME
{ mixin="fr.tolc.jahia.intellij.plugin.cnd.psi.identifiers.impl.CndNamespaceIdentifierElementImpl"
  implements="fr.tolc.jahia.intellij.plugin.cnd.psi.identifiers.CndNamespaceIdentifierElement"
  methods=[getNamespaceName setNamespaceName getNameIdentifier getPresentation getName setName getNamespace] }



nodeType ::= LEFT_BRACKET NAMESPACE_NAME COLON NODE_TYPE_NAME RIGHT_BRACKET [superTypes] [options] [COMMENT] [CRLF extensions] [COMMENT] [CRLF itemType] [COMMENT] [CRLF extensions] (CRLF+ COMMENT | CRLF+ property | CRLF+ subNode)* [CRLF]
{ mixin="fr.tolc.jahia.intellij.plugin.cnd.psi.impl.CndNodeTypeElementImpl"
  implements="fr.tolc.jahia.intellij.plugin.cnd.psi.CndNodeTypeElement" 
  methods=[getNodeTypeName setNodeTypeName getNodeTypeNamespace getNameIdentifier getPresentation getName setName getProperty] }

superTypes ::= RIGHT_ONLY_ANGLE_BRACKET superType (COMMA superType)* [COMMA]
superType ::= NAMESPACE_NAME COLON NODE_TYPE_NAME
{ mixin="fr.tolc.jahia.intellij.plugin.cnd.psi.impl.CndSuperTypeElementImpl"
  implements="fr.tolc.jahia.intellij.plugin.cnd.psi.CndSuperTypeElement"
  methods=[getReferences] }

options ::= ([CRLF] OPTION)+

extensions ::= EXTENDS EQUAL extension (COMMA extension)*
extension ::= NAMESPACE_NAME COLON NODE_TYPE_NAME
{ mixin="fr.tolc.jahia.intellij.plugin.cnd.psi.impl.CndExtensionElementImpl"
  implements="fr.tolc.jahia.intellij.plugin.cnd.psi.CndExtensionElement"
  methods=[getReferences] }

itemType ::= ITEMTYPE EQUAL ITEMTYPE_TYPE



property ::= MINUS propertyIdentifier [propertyType] [propertyDefault] [propertyAttributes] [propertyConstraint]
{ mixin="fr.tolc.jahia.intellij.plugin.cnd.psi.impl.CndPropertyElementImpl"
  implements="fr.tolc.jahia.intellij.plugin.cnd.psi.CndPropertyElement"
  methods=[getPropertyName setPropertyName getType getTypeMask getPresentation] }
propertyIdentifier ::= PROPERTY_NAME
{ mixin="fr.tolc.jahia.intellij.plugin.cnd.psi.identifiers.impl.CndPropertyIdentifierElementImpl"
  implements="fr.tolc.jahia.intellij.plugin.cnd.psi.identifiers.CndPropertyIdentifierElement"
  methods=[getPropertyName setPropertyName getNameIdentifier getPresentation getName setName getProperty] }
private propertyType ::= LEFT_PARENTHESIS PROPERTY_TYPE [COMMA PROPERTY_MASK [propertyMaskOptions]] RIGHT_PARENTHESIS
private propertyMaskOptions ::= LEFT_BRACKET propertyMaskOption (COMMA propertyMaskOption)* RIGHT_BRACKET 
private propertyMaskOption ::= PROPERTY_MASK_OPTION [EQUAL PROPERTY_MASK_OPTION_VALUE]

propertyDefault ::= EQUAL PROPERTY_DEFAULT_VALUE

propertyAttributes ::= PROPERTY_ATTRIBUTE+

propertyConstraint ::= LEFT_ONLY_ANGLE_BRACKET PROPERTY_CONSTRAINT_VALUE



subNode ::= PLUS NODE_NAME LEFT_PARENTHESIS subNodeTypes RIGHT_PARENTHESIS [subNodeDefault] [subNodeAttributes]
private subNodeTypes ::= subNodeType (COMMA subNodeType)*
subNodeType ::= NAMESPACE_NAME COLON NODE_TYPE_NAME
{ mixin="fr.tolc.jahia.intellij.plugin.cnd.psi.impl.CndSubNodeTypeElementImpl"
  implements="fr.tolc.jahia.intellij.plugin.cnd.psi.CndSubNodeTypeElement"
  methods=[getReferences] }

private subNodeDefault ::= EQUAL subNodeDefaultType
subNodeDefaultType ::= NAMESPACE_NAME COLON NODE_TYPE_NAME
{ mixin="fr.tolc.jahia.intellij.plugin.cnd.psi.impl.CndSubNodeDefaultTypeElementImpl"
  implements="fr.tolc.jahia.intellij.plugin.cnd.psi.CndSubNodeDefaultTypeElement"
  methods=[getReferences] }

subNodeAttributes ::= NODE_ATTRIBUTE+